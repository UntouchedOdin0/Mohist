plugins {
    id 'java'
}

version '1.17.1-' + MOHIST_VERSION

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.logging.log4j:log4j-api:2.14.1')
    implementation('org.apache.logging.log4j:log4j-core:2.14.1')
    implementation('net.minecrell:terminalconsoleappender:1.2.0')
    implementation('com.google.code.gson:gson:2.8.0')
    compileOnly('org.jetbrains:annotations:21.0.0')
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

ext {
    MANIFESTS = [
            '/': [
                    'Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Git-Commit': GIT_INFO.abbreviatedId,
                    'Git-Branch': GIT_INFO.branch,
                    'Build-Number': MOHIST_VERSION,
            ] as LinkedHashMap,
            'com/mohistmc/': [
                    'Specification-Title':      'MohistLoader',
                    'Specification-Vendor':     'MohistMC',
                    'Specification-Version':    '1',
                    'Implementation-Title':     'Mohist Loader',
                    'Implementation-Version':   '1.0',
                    'Implementation-Vendor':    'Mohist'
            ] as LinkedHashMap
    ]
}

jar.doFirst {
    MANIFESTS.each { pkg, values ->
        manifest.attributes(values, pkg)
    }
}